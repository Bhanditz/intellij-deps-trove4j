plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven-publish'

jar {
    baseName 'trove4j'
}

task(generateTemplates, type: JavaExec, dependsOn: ':generator:compileJava') {
    main = 'gnu.trove.generate.Generate'
    classpath = findProject(':generator').sourceSets.main.runtimeClasspath
    args = [ 'src/main/templates', "${buildDir}/generated-src" ]
    workingDir = getProjectDir()
}

sourceSets {
    main {
        java.srcDirs += "${buildDir}/generated-src"
    }
}

compileJava.dependsOn(generateTemplates)


sourceCompatibility = '1.5'
targetCompatibility = '1.5'

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'org.jetbrains.trove4j'
            artifactId 'trove4j'
            version '20160824'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'trove4j'
        name = 'trove4j'
        userOrg = 'jetbrains'
        licenses = ['LGPL-2.1']
        vcsUrl = 'https://github.com/JetBrains/trove4j'

        version {
            name = '20160824'
        }
    }

    publications = ['MyPublication']
}
