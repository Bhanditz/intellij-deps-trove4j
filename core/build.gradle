plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'maven-publish'

ext.projectVersion = projectVersion

jar {
    baseName 'trove4j'
}

task(generateTemplates, type: JavaExec, dependsOn: ':generator:compileJava') {
    main = 'gnu.trove.generate.Generate'
    classpath = findProject(':generator').sourceSets.main.runtimeClasspath
    args = [ 'src/main/templates', "${buildDir}/generated-src" ]
    workingDir = getProjectDir()
}

sourceSets {
    main {
        java.srcDirs += "${buildDir}/generated-src/src"
    }
}

compileJava.dependsOn(generateTemplates)


sourceCompatibility = '1.5'
targetCompatibility = '1.5'

task sourceJar(type: Jar) {
    from sourceSets.main.java
}

javadoc {
    source = [sourceSets.main.java]
    options.addStringOption("Xdoclint:none", "-quiet")
}

task javadocJar(type: Jar) {
    from javadoc
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'org.jetbrains.intellij.deps'
            artifactId 'trove4j'
            version rootProject.ext.projectVersion
            artifact sourceJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name 'Trove4J'
                    description 'Fork of trove4j library used in IntelliJ Platform.'
                    url 'https://github.com/JetBrains/intellij-deps-trove4j'
                    scm {
                        url 'https://github.com/JetBrains/intellij-deps-trove4j'
                        connection 'scm:git:git://github.com/JetBrains/intellij-deps-trove4j.git'
                        developerConnection 'scm:git:ssh://github.com:JetBrains/intellij-deps-trove4j.git'
                    }
                    licenses {
                        license {
                            name 'GNU LESSER GENERAL PUBLIC LICENSE 2.1'
                            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'JetBrains'
                            name 'JetBrains Team'
                            organization 'JetBrains'
                            organizationUrl 'https://www.jetbrains.com'
                        }
                    }
                }
            }

        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publish = true
    
    pkg {
        repo = 'intellij-third-party-dependencies'
        name = 'trove4j'
        userOrg = 'jetbrains'

        version {
            name = rootProject.ext.projectVersion
        }
    }

    publications = ['MyPublication']
}
